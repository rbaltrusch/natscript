# Finds the index of an element in a sorted array using binary search
# Author: Richard Baltrusch
# Date: 14/04/2022

define function get_half_length expecting [collection] as {
    # returns half the length of collection
    set len to length of collection, divide it by 2, round it and return it
}


define function binary_search expecting [collection target] as {
    # returns the index of the target element in a sorted collection
    set index to result of call get_half_length with [collection]

    get number from collection at index
    if checked that the number is equal to the target then {
        return index
    }

    if checked that the target is greater than the number then {
        set split_collection to slice of collection from index to length of collection
        call binary_search with [split_collection, target]
        add result to index and return it
    }

    set split_collection to slice of collection from 0 to index
    return result of call binary_search with [split_collection, target]
}


define function main as {
    # main function
    set collection to [1, 3, 5, 8, 13, 26, 53, 76, 88, 135, 268, 377, 416, 529, 876, 1000]
    for each target in collection {
        call binary_search with [collection, target] and print result
    }
}

call main
